// Fox and Hounds (Wolf and Sheep)
// -- The player that is unable to move loses.
// -- Fox wins when reaches the hounds' back rank.
// -- In the first turn, fox selects any initial square of the four in his back rank.

#players = foxPlayer(100), houndPlayer(100)
#pieces = e, f, h
#variables =
#board = rectangle(upRight,downLeft,upLeft,downRight,
         [ , , ,e, , , ]
         [ , ,e,e,e, , ]
         [ ,e,e,e,e,e, ]
         [e,e,e,e,e,e,e]
         [e,e,e,e,e,e,h]
         [ ,e,e,e,e,h, ]
         [ , ,e,e,h, , ]
         [ , , ,h, , , ])

#anyRect = (upRight* + downLeft*)(upLeft* + downRight*)
#gotoCenter = anyRect {? upRight^3 downLeft^4}
#anySquare = (gotoCenter anyRect)

#foxTurn(forward) = (
    ->> anySquare {f} [e]
    ->foxPlayer (upRight + downLeft + upLeft + downRight) {e}
    ->> [f] [$ foxPlayer = 100, houndPlayer = 0]
    (   {? forward~Left + forward~Right}
      + {! forward~Left + forward~Right} ->> {})
  )

#houndTurn(forward) = (
    ->houndPlayer anySquare {h} [e] (forward~Left + forward~Right) {e}
    ->> [h] [$ foxPlayer = 0, houndPlayer = 100]
  )

#rules = ->foxPlayer anySquare {! upLeft + upRight} ->> [f]
    (foxTurn(down) houndTurn(up))*
